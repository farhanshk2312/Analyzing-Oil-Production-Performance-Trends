---------------------------------------------
-- Calculated Table: 'Measures Table Revenue'
---------------------------------------------
TABLE 'Measures Table Revenue' = DATATABLE("Placeholder", STRING, {{"Keep"}})

-----------------------------
-- Measure: [Current Revenue]
-----------------------------
MEASURE 'Measures Table Revenue'[Current Revenue] = 
    CALCULATE(
        SUM('production_hierarchy'[Revenue]),
        LASTDATE('Calendar'[Date])
    )
    , FormatString = "\$#,0.00;(\$#,0.00);\$#,0.00"

------------------------------------
-- Measure: [Previous Month Revenue]
------------------------------------
MEASURE 'Measures Table Revenue'[Previous Month Revenue] = 
    CALCULATE(
        [Total Revenue],
        DATEADD(
            'Calendar'[Date],
            -1,
            MONTH
        )
    )

---------------------------
-- Measure: [Total Revenue]
---------------------------
MEASURE 'Measures Table Revenue'[Total Revenue] = Sum('production_hierarchy'[Revenue])
    , FormatString = "\$#,0;(\$#,0);\$#,0"

------------------------------
-- Measure: [Revenue Growth %]
------------------------------
MEASURE 'Measures Table Revenue'[Revenue Growth %] = DIVIDE([Total Revenue] - [Previous Month Revenue], [Previous Month Revenue])

----------------------------------
-- Measure: [Revenue Growth Arrow]
----------------------------------
MEASURE 'Measures Table Revenue'[Revenue Growth Arrow] = 
    var uparrow = UNICHAR(129093)
    var downarrow = UNICHAR(129095)
    var revenuegrowth = [Revenue Growth %]
    
    RETURN
    IF(revenuegrowth < 0,
     FORMAT(ROUND(revenuegrowth * 100, 2), "0.00") & "%" & " " & downarrow,
        FORMAT(ROUND(revenuegrowth * 100, 2), "0.00") & "%" & " " & uparrow
    )

-----------------------------------
-- Measure: [Previous Year Revenue]
-----------------------------------
MEASURE 'Measures Table Revenue'[Previous Year Revenue] = 
    CALCULATE (
        SUM ( production_hierarchy[Revenue] ),
        FILTER (
            ALL ( 'Calendar' ),
            'Calendar'[Year] = YEAR ( TODAY() ) - 1
        )
    )
    , FormatString = "0"

------------------------------------
-- Measure: [Revenue Same Period LY]
------------------------------------
MEASURE 'Measures Table Revenue'[Revenue Same Period LY] = 
    CALCULATE(
        [Total Revenue],
        DATEADD(
            'Calendar'[Date],
            -1,
            YEAR))

----------------------------------
-- Measure: [Revenue Growth YOY %]
----------------------------------
MEASURE 'Measures Table Revenue'[Revenue Growth YOY %] = DIVIDE([Total Revenue] - [Revenue Same Period LY], [Revenue Same Period LY])

--------------------------------------
-- Measure: [Revenue Growth YOY Arrow]
--------------------------------------
MEASURE 'Measures Table Revenue'[Revenue Growth YOY Arrow] = 
    var uparrow = UNICHAR(129093)
    var downarrow = UNICHAR(129095)
    var revenuegrowth = [Revenue Growth YOY %]
    
    RETURN
    IF(revenuegrowth < 0,
     FORMAT(ROUND(revenuegrowth * 100, 2), "0.00") & "%" & " " & downarrow,
        FORMAT(ROUND(revenuegrowth * 100, 2), "0.00") & "%" & " " & uparrow
    )

------------------------------------
-- Measure: [Rolling 3M Avg Revenue]
------------------------------------
MEASURE 'Measures Table Revenue'[Rolling 3M Avg Revenue] = 
    AVERAGEX (
        DATESINPERIOD (
            'Calendar'[Date],
            MAX ( 'Calendar'[Date] ),
            -3,
            MONTH
        ),
        CALCULATE ( SUM ( 'production_hierarchy'[Revenue] ) )
    )
    , FormatString = "\$#,0.00;(\$#,0.00);\$#,0.00"

------------------------------------
-- Measure: [Rolling 6M Avg Revenue]
------------------------------------
MEASURE 'Measures Table Revenue'[Rolling 6M Avg Revenue] = 
    AVERAGEX (
        DATESINPERIOD (
            'Calendar'[Date],
            MAX ( 'Calendar'[Date] ),
            -6,
            MONTH
        ),
        CALCULATE ( SUM ( 'production_hierarchy'[Revenue] ) )
    )
    , FormatString = "\$#,0.00;(\$#,0.00);\$#,0.00"

-------------------------------------
-- Measure: [Rolling 12M Avg Revenue]
-------------------------------------
MEASURE 'Measures Table Revenue'[Rolling 12M Avg Revenue] = 
    AVERAGEX (
        DATESINPERIOD (
            'Calendar'[Date],
            MAX ( 'Calendar'[Date] ),
            -12,
            MONTH
        ),
        CALCULATE ( SUM ( 'production_hierarchy'[Revenue] ) )
    )
    , FormatString = "\$#,0.00;(\$#,0.00);\$#,0.00"

-------------------------------------------
-- Measure: [Deviation from 3M Avg Rev (%)]
-------------------------------------------
MEASURE 'Measures Table Revenue'[Deviation from 3M Avg Rev (%)] = DIVIDE([Current Revenue] - [Rolling 3M Avg Revenue], [Rolling 3M Avg Revenue], 0)
    , FormatString = "0.00%;-0.00%;0.00%"

-------------------------------------------
-- Measure: [Deviation from 6M Avg Rev (%)]
-------------------------------------------
MEASURE 'Measures Table Revenue'[Deviation from 6M Avg Rev (%)] = DIVIDE([Current Revenue] - [Rolling 6M Avg Revenue], [Rolling 6M Avg Revenue], 0)
    , FormatString = "0.00%;-0.00%;0.00%"

--------------------------------------------
-- Measure: [Deviation from 12M Avg Rev (%)]
--------------------------------------------
MEASURE 'Measures Table Revenue'[Deviation from 12M Avg Rev (%)] = DIVIDE([Current Revenue] - [Rolling 12M Avg Revenue], [Rolling 12M Avg Revenue], 0)
    , FormatString = "0.00%;-0.00%;0.00%"

--------------------------
-- Measure: [CAGR Revenue]
--------------------------
MEASURE 'Measures Table Revenue'[CAGR Revenue] = 
    VAR CompleteYears =
        FILTER (
            VALUES ( 'production_hierarchy'[Year] ),
            CALCULATE (
                DISTINCTCOUNT ( 'production_hierarchy'[YearMonth] ),
                'production_hierarchy'[Revenue] > 0
            ) = 12
        )
    VAR StartYear = MINX ( CompleteYears, 'production_hierarchy'[Year] )
    VAR EndYear   = MAXX ( CompleteYears, 'production_hierarchy'[Year] )
    VAR StartRevenue =
        CALCULATE (
            SUM ( 'production_hierarchy'[Revenue] ),
            'production_hierarchy'[Year] = StartYear
        )
    VAR EndRevenue =
        CALCULATE (
            SUM ( 'production_hierarchy'[Revenue] ),
            'production_hierarchy'[Year] = EndYear
        )
    VAR NumYears = EndYear - StartYear
    RETURN
    IF (
        NOT ISBLANK(StartYear) &&
        NOT ISBLANK(EndYear) &&
        NumYears > 0 &&
        StartRevenue > 0,
        ( ( EndRevenue / StartRevenue ) ^ ( 1 / NumYears ) - 1 ),
        BLANK()
    )
    , FormatString = "0.00%;-0.00%;0.00%"

----------------------------
-- Measure: [Complete Years]
----------------------------
MEASURE 'Measures Table Revenue'[Complete Years] = 
    FILTER (
        VALUES ( 'production_hierarchy'[Year] ),
        CALCULATE ( DISTINCTCOUNT ( 'production_hierarchy'[YearMonth] ) ) = 12
    )
    , FormatString = "0"

----------------------------
-- Measure: [StdDev Revenue]
----------------------------
MEASURE 'Measures Table Revenue'[StdDev Revenue] = 
    VAR MonthlyRevenue =
        SUMMARIZE (
            'production_hierarchy',
            'production_hierarchy'[Year],
            'production_hierarchy'[YearMonth],
            "MonthlyTotal", SUM ( 'production_hierarchy'[Revenue] )
        )
    RETURN
    STDEVX.P ( MonthlyRevenue, [MonthlyTotal] )
    , FormatString = "#,0.00"

------------------------------------------
-- Measure: [Coefficient of Rev Variation]
------------------------------------------
MEASURE 'Measures Table Revenue'[Coefficient of Rev Variation] = 
    VAR MonthlyRevenue =
        SUMMARIZE (
            'production_hierarchy',
            'production_hierarchy'[Year],
            'production_hierarchy'[YearMonth],
            "MonthlyTotal", SUM ( 'production_hierarchy'[Revenue] )
        )
    VAR Std_Dev = STDEVX.P ( MonthlyRevenue, [MonthlyTotal] )
    VAR Avg_Rev = AVERAGEX ( MonthlyRevenue, [MonthlyTotal] )
    RETURN
    DIVIDE ( Std_Dev, Avg_Rev )
    , FormatString = "0.00%;-0.00%;0.00%"

---------------------------------------
-- Measure: [Revenue Consistency Score]
---------------------------------------
MEASURE 'Measures Table Revenue'[Revenue Consistency Score] = 
    VAR MonthlyRevenue =
        SUMMARIZE (
            'production_hierarchy',
            'production_hierarchy'[Year],
            'production_hierarchy'[YearMonth],
            "MonthlyTotal", SUM ( 'production_hierarchy'[Revenue] )
        )
    VAR AvgRev = AVERAGEX ( MonthlyRevenue, [MonthlyTotal] )
    VAR LowerBound = AvgRev * 0.9
    VAR UpperBound = AvgRev * 1.1
    VAR TotalMonths = COUNTROWS ( MonthlyRevenue )
    VAR StableMonths =
        COUNTROWS (
            FILTER (
                MonthlyRevenue,
                [MonthlyTotal] >= LowerBound &&
                [MonthlyTotal] <= UpperBound
            )
        )
    RETURN
    DIVIDE ( StableMonths, TotalMonths )
    , FormatString = "0.00%;-0.00%;0.00%"

------------------------------
-- Measure: [Decline Rate Rev]
------------------------------
MEASURE 'Measures Table Revenue'[Decline Rate Rev] = 
    VAR CurrentMonth = SUM('production_hierarchy'[Revenue])
    VAR PrevMonth =
        CALCULATE(
            SUM('production_hierarchy'[Revenue]),
            DATEADD('production_hierarchy'[Date], -1, MONTH)
        )
    RETURN
    IF(
        NOT ISBLANK(PrevMonth),
        DIVIDE(CurrentMonth - PrevMonth, PrevMonth) * 100
    )
    , FormatString = "0.00%;-0.00%;0.00%"

-------------------------
-- Measure: [YTD Revenue]
-------------------------
MEASURE 'Measures Table Revenue'[YTD Revenue] = 
    TOTALYTD(
        SUM('production_hierarchy'[Revenue]),
        'Calendar'[Date]
    )

--------------------------
-- Measure: [PYTD revenue]
--------------------------
MEASURE 'Measures Table Revenue'[PYTD revenue] = 
    CALCULATE(
        [YTD Revenue],
        SAMEPERIODLASTYEAR('Calendar'[Date])
    )

-------------------------------------
-- Measure: [YOY % (YTD vs PYTD) Rev]
-------------------------------------
MEASURE 'Measures Table Revenue'[YOY % (YTD vs PYTD) Rev] = 
    DIVIDE(
        [YTD Revenue] - [PYTD revenue],
        [PYTD revenue]
    )
    , FormatString = "0.00%;-0.00%;0.00%"